varnishtest "synth.template()"

server s1 {
   rxreq
   txresp
} -start

shell {
    echo -n "Hello {{ name }} {{ name }} {{ name }}!" > ${tmpdir}/test.txt.tmpl
}

varnish v1 -vcl+backend {
    import synth from "${vmod_topbuild}/src/.libs/libvmod_synth.so";

    sub vcl_recv {
        error 700;
    }

    sub vcl_error {
        set obj.status = 200;
        set obj.response = "OK";
        set obj.http.Content-Type = "text/plain";
        synth.template(
            "${tmpdir}/test.txt.tmpl",
            "{{ name }}|" + req.http.Name);
        return (deliver);
    }
} -start

client c1 {
    txreq -url "/" -hdr "Name: world"
    rxresp
    expect resp.body == "Hello world world world!"
} -run

varnish v1 -expect client_req == 1
